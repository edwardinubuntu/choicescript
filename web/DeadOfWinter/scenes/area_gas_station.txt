*label start
*image gas_station.JPG
加油站

加油站外幾座加油台似乎還有燃料，店裡面招牌與架上商品標籤寫著，這地方以前是販售過日常工具與食物。
*if (resource_gas_station_left>0)
  還有物資，還剩下多少就要探索看看了。
*if (resource_gas_station_left<=0)
  空空蕩蕩沒有物資了。
*line_break
這區總共有 ${max_gas_station_entrance_count} 個出入口，
*if (current_gas_station_zombies_count>0)
  擠了 ${current_gas_station_zombies_count} 個殭屍，
目前搭建了 ${current_gas_station_barricade_count} 個柵欄。

*temp area "gas station"
*set user_stay_area area

*if (action_per_round > 0)
  還有 ${action_per_round} 次行動點，你打算怎麼做？
*if (action_per_round <= 0)
  沒有行動了，還可以做一些事情，或者選擇休息，本日結束。

*choice
  *if ((is_moving_parkour = true) and (action_per_round>0)) #跑酷搜索
    *goto_scene search_resource
  *selectable_if (action_per_round>0) #搜索
    *goto_scene search_resource
  *selectable_if ((action_per_round>0) and (current_gas_station_barricade_count<max_gas_station_entrance_count)) #蓋柵欄
    *set action_per_round action_per_round-1
    *set current_gas_station_barricade_count current_gas_station_barricade_count+build_barricade_per_time
    你搭建了 ${build_barricade_per_time} 個柵欄，這區總共有了 ${current_gas_station_barricade_count} 柵欄。
  	
    *gosub build_barricade  
  *selectable_if (current_gas_station_zombies_count>0) #攻擊殭屍
    *goto_scene kill_zombie  
  #離開此處
    *goto_scene area_move    
  #休息，回合結束
    *goto_scene check
    
*label build_barricade
*choice
  #建置完成
    *goto start    