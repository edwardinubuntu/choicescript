*label start
*image colony2.JPG
*set round_turn_food_need round((current_helpless_survivor + current_action_survivor) / 2)
*set round_turn_zombie_need round_turn_food_need
第 ${round_play+1} 日，避難所大廳

有 ${current_helpless_survivor} 位無助幸存者和 ${current_action_survivor} 幸存者在這邊。
*if (current_colony_food>=round_turn_food_need)
  在食物儲藏室的食物供給足夠。
*if (current_colony_food<round_turn_food_need)
  在食物儲藏室的食物供給不足，要飢餓了，我們該怎麼辦才好。
*comment 前院狀況
*line_break
往窗外看出去，前面庭院廣場到圍牆，
*if (current_colony_zombies_count>0)
  擠了一群殭屍在徘徊走動，時而發出低成嘶吼聲音。
*if (current_colony_zombies_count = 0)
  非常安靜，除了吹來風聲以外，毫無任何生跡。
*if (current_colony_barricade_count>0)
  目前有些許柵欄，希望可以抵擋住殭屍別往裡面來。
*if (current_colony_barricade_count<=0)
  在沒有任何柵欄保護，殭屍隨時會走進來，等空間滿了就非常危險。
*comment 後院狀況
*line_break
*if (current_trash>0)
  後院的垃圾堆開始有垃圾，希望垃圾別滿，不然環境的衛生就會出問題。
*if (current_trash=0)
  後院的垃圾堆是非常乾淨的。  

剩下${Round}天。

*temp area "colony"
*set user_stay_area area
*temp trash_increase 1

*if (action_per_round > 0)
  還有 ${action_per_round} 次行動點，你打算怎麼做？
*if (action_per_round <= 0)
  沒有行動了，還可以做一些事情，或者選擇休息，本日結束。
  
*choice
  *if (has_gathering = false) #認識所有倖存者(只有1次)
    *set has_gathering true
    *goto_scene gathering  
  #處理本日危機
    *goto_scene thread
  #前院檢查柵欄
    *goto colony_border
  *selectable_if (current_colony_zombies_count>0) #外圍攻擊殭屍
    *goto_scene kill_zombie
  #後院廢棄物堆
    *goto colony_backyard
  #到食物儲藏室
    *goto food_shelf_room
  #到醫護室
    *goto heal_room  
  #離開避難所，外出行動
    *goto_scene area_move   
  #休息，本日結束
    *goto_scene check

*label colony_border
前院

昨天這裡還是個難民營，今天就成了有模有樣的城鎮。如今人們稱這裡為避難所時的語氣已經沒有當時的挖苦與諷刺，它確實庇護著我們。唯一問題是，我們所有辛勤工作製造聲音，也給我們吸引來了大群的殭屍。每天都在我們的牆外遊蕩，伺機尋找新鮮的肉食。

庭院外面到圍牆，總共有  ${max_colony_entrance_count} 出入口空間，擠了 ${current_colony_zombies_count} 個殭屍，目前有 ${current_colony_barricade_count} 柵欄。依照目前狀況，我們每天估計會吸引到 ${round_turn_zombie_need} 位殭屍。
*if (tool_for_player > 0)
  *line_break
  用工具蓋柵欄可以蓋比較快，消耗工具會變成垃圾，但是不會耗用到行動。
*temp can_build_barricade (max_colony_entrance_count > (current_colony_zombies_count+current_colony_barricade_count))
*choice
  *if ((tool_for_player > 0) and can_build_barricade) #用工具蓋柵欄
    *set current_colony_barricade_count current_colony_barricade_count+build_barricade_per_time
    *set tool_for_player tool_for_player-1
    *set current_trash current_trash+trash_increase
    你搭建了 ${build_barricade_per_time} 個柵欄，將工具丟到垃圾堆，讓避難所總共有了 ${current_colony_barricade_count} 柵欄。
    *gosub finish_build_barricade
  *selectable_if ((action_per_round > 0) and can_build_barricade) #蓋柵欄
    *set action_per_round action_per_round-1
    *set current_colony_barricade_count current_colony_barricade_count+build_barricade_per_time
    你搭建了 ${build_barricade_per_time} 個柵欄，讓避難所總共有了 ${current_colony_barricade_count} 柵欄。
    *goto finish_build_barricade 
  #回到避難所大廳
    *goto_scene colony

*label colony_backyard
後院

*if (current_trash>0)
  垃圾堆開始有垃圾，垃圾填滿了 ${current_trash/max_trash_count}  整個空間，希望垃圾別滿，不然環境的衛生就會出問題。
*if (current_trash=0)
  垃圾堆是非常乾淨的。
*choice
  *selectable_if ((action_per_round>0) and (current_trash>0)) #整理垃圾堆
    *set action_per_round action_per_round-1
    *set current_trash current_trash-remove_trash_count
    *if (current_trash<0)
      *set current_trash 0
    你耗費了些許時間移除了 ${remove_trash_count} 個垃圾，讓垃圾堆剩下 ${current_trash} 個垃圾。
    *goto colony_backyard
  #回到避難所大廳
    *goto start

*label back_to_colony
*choice
  #回到避難所大廳
    *goto_scene colony
    
*label heal_room
醫護室

*if (lose_blood <= 0)
  目前一切尚可，狀況良好。
*if (lose_blood > 0)
  你有些許受傷，建議趕緊醫療。

*if (medicine_for_player <= 0)
  查看了隨身醫藥用品，空的。無法在此醫療。  
*if (medicine_for_player > 0)
  目前還有 ${medicine_for_player} 份醫療用品，可以在此醫療。

*choice
  *selectable_if ((medicine_for_player > 0) and (lose_blood > 0)) #醫療自己
    *set lose_blood lose_blood-1
    *set medicine_for_player medicine_for_player-1
    *set current_trash current_trash+trash_increase
    *goto finish_heal
  #返回避難所大廳
    *goto start

*label food_shelf_room

食物供給堆

我們有 ${current_helpless_survivor+current_action_survivor} 位生存者在避難所，在儲藏架上食物只有 ${current_colony_food} 份。算了一下每天都需要用到 ${round_turn_food_need} 份食物。
*if(current_colony_food<round_turn_food_need)
  不夠呀。

*temp area "colony"
*set user_stay_area area

*temp give_food 0

*choice
  *if (((has_use_cooking_mom=false) and (is_cooking_mom=true)) and (action_per_round>0)) #廚師佳餚
    *set current_colony_food current_colony_food+2
    *set action_per_round action_per_round-1
    *set has_use_cooking_mom true
    *gosub finish_cook_food
  *selectable_if (food_for_player > 0) #提供食物
    你擁有 ${food_for_player} 份隨行食物，想要提供多少呢。

    *input_number give_food 0 food_for_player
    
    *set current_colony_food current_colony_food+give_food
    *set food_for_player food_for_player-give_food
    *if (give_food>0)
      *set current_trash current_trash+trash_increase

    *goto finish_give_food
  #返回避難所大廳
    *goto start
    
*label finish_build_barricade
*choice
  #完成
    *gosub colony_border
        
*label finish_give_food
你提供了 ${give_food} 份食物到食物供給堆上。
*if (give_food > 0)
  丟了 ${trash_increase} 份垃圾。
*choice
  #完成
    *gosub food_shelf_room

*label finish_heal
花費了 1 個藥品，減少了傷口疼痛。丟了 ${trash_increase} 份垃圾。
*choice
  #完成
    *gosub heal_room  
    
*label finish_cook_food
花費了 1 次行動，提供了 2 份食物到食物供給堆上。
*choice
  #完成
    *gosub food_shelf_room
